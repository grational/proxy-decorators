package it.italiaonline.rnd.proxy

import java.net.Proxy.Type

/**
 * This Groovy source file was auto generated by 'gigawatt'
 * @author d7392
 * @date 01-08-2018 12.55
 */
class EnvProxy {

	private final Protocol protocol
	private final String   envVar

	EnvProxy(Protocol sp) {
		this.protocol = sp
		this.envVar   = System.getenv().get(sp.envVar())
	}

	Proxy.Type type() {
		def type
		if ( this.envVar == null )
			type = Proxy.Type.DIRECT
		else if ( this.envVar ==~ 'http://[^:]+:[1-9][0-9]{0,4}' )
			type = Proxy.Type.HTTP
		else if ( this.envVar ==~ 'socks[45]://[^:]+:[1-9][0-9]{0,4}' )
			type = Proxy.Type.SOCKS
		else
			throw new UnsupportedOperationException("Invalid proxy type '${this.envVar}'")
	}

	String host() {
		def host
		switch( this.type() ) {
			case Proxy.Type.HTTP:
				host = this.envVar.find($/(?<=^http://)[^:]+/$)
				break
			case Proxy.Type.SOCKS:
				host = this.envVar.find($/(?<=^socks[45]://)[^:]+/$)
				break
			case Proxy.Type.DIRECT:
				throw new UnsupportedOperationException('Cannot return a host with Proxy.Type.DIRECT')
		}
		return host
	}

	Integer port() {
		if ( this.type() == Proxy.Type.DIRECT )
			throw new UnsupportedOperationException('Cannot return a port with Proxy.Type.DIRECT')

		return this.envVar.find($/(?<=:)[1-9][0-9]{0,4}/$) as Integer
	}
}
